#lang plait

;; true = {lambda {x} {lambda (y} x}}
;; false = {lambda {x} {lambda (y} y}}
;; {if E1 E2 E3} = {{{E1 {lambda {d} E2}} {lambda {d} E3}} 0}

;; {pair E1 E2} = {lambda {p} {if p E1 E2}}
;; {fst EP} = {EP true} 
;; {snd EP} = {EP false}

;; ----------------------------------------

;; A list is different from a pair because it has two cases: empty and cons.
;; So, using a pair that starts with false for empty and true for cons.

;; empty = {pair false false}
;; {cons E1 E2} = {pair true {pair E1 E2}}

;; {cons? E1} = {fst E1}
;; {empty? E1} = {if {fst E1} false true}

;; {first E1} = {fst {snd E1}}
;; {rest E1} = {snd {snd E1}}

;; Note that first of emtpy will do something strange, because you're
;; not supposed to take the first of the empty list.

(define-type Value
  (numV [n : Number])
  (closV [arg : Symbol]
         [body : Exp]
         [env : Env]))

(define-type Exp
  (numE [n : Number])
  (idE [s : Symbol])
  (plusE [l : Exp] 
         [r : Exp])
  (multE [l : Exp]
         [r : Exp])
  (lamE [n : Symbol]
        [body : Exp])
  (appE [fun : Exp]
        [arg : Exp]))

(define-type Binding
  (bind [name : Symbol]
        [val : Value]))

(define-type-alias Env (Listof Binding))

(define mt-env empty)
(define extend-env cons)

(module+ test
  (print-only-errors #t))

;; parse ----------------------------------------
(define (parse [s : S-Exp]) : Exp
  (cond
    ;; Encoding lists in terms of booleans and pairs:
    [(s-exp-match? `empty s) (parse `{pair false false})]
    [(s-exp-match? `{cons ANY ANY} s)
     (parse `{pair true {pair ,(second (s-exp->list s)) ,(third (s-exp->list s))}})]
    [(s-exp-match? `{empty? ANY} s) (parse `{if {fst ,(second (s-exp->list s))} false true})]
    [(s-exp-match? `{cons? ANY} s) (parse `{fst ,(second (s-exp->list s))})]
    [(s-exp-match? `{first ANY} s) (parse `{fst {snd ,(second (s-exp->list s))}})]
    [(s-exp-match? `{rest ANY} s) (parse `{snd {snd ,(second (s-exp->list s))}})]
    ;; booleans
    [(s-exp-match? `true s) (parse `{lambda {x} {lambda {y} x}})]
    [(s-exp-match? `false s) (parse `{lambda {x} {lambda {y} y}})]
    [(s-exp-match? `{if ANY ANY ANY} s)
     (local[(define E1(second (s-exp->list s)))
            (define E2(third (s-exp->list s)))
            (define E3(fourth (s-exp->list s)))]
       (parse `{{{,E1 {lambda {d} ,E2}} {lambda {d} ,E3}} 0}))]
    ;; pairs
    [(s-exp-match? `{pair ANY ANY} s)
     (local [(define E1 (second (s-exp->list s)))
             (define E2 (third (s-exp->list s)))]
       (parse `{lambda {p} {if p ,E1 ,E2}}))]
    [(s-exp-match? `{fst ANY} s)
     (local [(define EP (second (s-exp->list s)))]
       (parse `{,EP true}))]
    [(s-exp-match? `{snd ANY} s)
     (local [(define EP (second (s-exp->list s)))]
       (parse `{,EP false}))]
    ;; original
    [(s-exp-match? `NUMBER s) (numE (s-exp->number s))]
    [(s-exp-match? `SYMBOL s) (idE (s-exp->symbol s))]
    [(s-exp-match? `{+ ANY ANY} s)
     (plusE (parse (second (s-exp->list s)))
            (parse (third (s-exp->list s))))]
    [(s-exp-match? `{* ANY ANY} s)
     (multE (parse (second (s-exp->list s)))
            (parse (third (s-exp->list s))))]
    [(s-exp-match? `{let {[SYMBOL ANY]} ANY} s)
     (let ([bs (s-exp->list (first
                             (s-exp->list (second
                                           (s-exp->list s)))))])
       (appE (lamE (s-exp->symbol (first bs))
                   (parse (third (s-exp->list s))))
             (parse (second bs))))]
    [(s-exp-match? `{lambda {SYMBOL} ANY} s)
     (lamE (s-exp->symbol (first (s-exp->list 
                                  (second (s-exp->list s)))))
           (parse (third (s-exp->list s))))]
    [(s-exp-match? `{ANY ANY} s)
     (appE (parse (first (s-exp->list s)))
           (parse (second (s-exp->list s))))]
    [else (error 'parse "invalid input")]))

(module+ test
  (test (parse `2)
        (numE 2))
  (test (parse `x)
        (idE 'x))
  (test (parse `{+ 2 1})
        (plusE (numE 2) (numE 1)))
  (test (parse `{* 3 4})
        (multE (numE 3) (numE 4)))
  (test (parse `{+ {* 3 4} 8})
        (plusE (multE (numE 3) (numE 4))
               (numE 8)))
  (test (parse `{let {[x {+ 1 2}]}
                  y})
        (appE (lamE 'x (idE 'y))
              (plusE (numE 1) (numE 2))))
  (test (parse `{lambda {x} 9})
        (lamE 'x (numE 9)))
  (test (parse `{double 9})
        (appE (idE 'double) (numE 9)))
  (test/exn (parse `{{+ 1 2}})
            "invalid input")

  ;; true = {lambda {x} {lambda (y} x}}
  (test (parse `true) (lamE 'x (lamE 'y (idE 'x))))

  ;; false = {lambda {x} {lambda (y} y}}
  (test (parse `false) (lamE 'x (lamE 'y (idE 'y)))))

;; interp ----------------------------------------
(define (interp [a : Exp] [env : Env]) : Value
  (type-case Exp a
    [(numE n) (numV n)]
    [(idE s) (lookup s env)]
    [(plusE l r) (num+ (interp l env) (interp r env))]
    [(multE l r) (num* (interp l env) (interp r env))]
    [(lamE n body) (closV n body env)]
    [(appE fun arg) (type-case Value (interp fun env)
                      [(closV n body c-env)
                       (interp body
                               (extend-env
                                (bind n
                                      (interp arg env))
                                c-env))]
                      [else (error 'interp "not a function")])]))

(module+ test
  (test (interp (parse `2) mt-env)
        (numV 2))
  (test/exn (interp (parse `x) mt-env)
            "free variable")
  (test (interp (parse `x) 
                (extend-env (bind 'x (numV 9)) mt-env))
        (numV 9))
  (test (interp (parse `{+ 2 1}) mt-env)
        (numV 3))
  (test (interp (parse `{* 2 1}) mt-env)
        (numV 2))
  (test (interp (parse `{+ {* 2 3} {+ 5 8}})
                mt-env)
        (numV 19))
  (test (interp (parse `{lambda {x} {+ x x}})
                mt-env)
        (closV 'x (plusE (idE 'x) (idE 'x)) mt-env))
  (test (interp (parse `{let {[x 5]}
                          {+ x x}})
                mt-env)
        (numV 10))
  (test (interp (parse `{let {[x 5]}
                          {let {[x {+ 1 x}]}
                            {+ x x}}})
                mt-env)
        (numV 12))
  (test (interp (parse `{let {[x 5]}
                          {let {[y 6]}
                            x}})
                mt-env)
        (numV 5))
  (test (interp (parse `{{lambda {x} {+ x x}} 8})
                mt-env)
        (numV 16))
  (test (interp (parse `{if true 1 2}) mt-env)
        (numV 1))
  (test (interp (parse `{if false 1 2}) mt-env)
        (numV 2))
  (test (interp (parse `{fst {pair 1 2}}) mt-env) (numV 1))
  (test (interp (parse `{snd {pair 1 2}}) mt-env) (numV 2))
  (test/exn (interp (parse `{1 2}) mt-env)
            "not a function")
  (test/exn (interp (parse `{+ 1 {lambda {x} x}}) mt-env)
            "not a number")
  (test/exn (interp (parse `{let {[bad {lambda {x} {+ x y}}]}
                              {let {[y 5]}
                                {bad 2}}})
                    mt-env)
            "free variable")

  #;
  (time (interp (parse '{let {[x2 {lambda {n} {+ n n}}]}
                          {let {[x4 {lambda {n} {x2 {x2 n}}}]}
                            {let {[x16 {lambda {n} {x4 {x4 n}}}]}
                              {let {[x256 {lambda {n} {x16 {x16 n}}}]}
                                {let {[x65536 {lambda {n} {x256 {x256 n}}}]}
                                  {x65536 1}}}}}})
                mt-env)))

;; num+ and num* ----------------------------------------
(define (num-op [op : (Number Number -> Number)] [l : Value] [r : Value]) : Value
  (cond
    [(and (numV? l) (numV? r))
     (numV (op (numV-n l) (numV-n r)))]
    [else
     (error 'interp "not a number")]))
(define (num+ [l : Value] [r : Value]) : Value
  (num-op + l r))
(define (num* [l : Value] [r : Value]) : Value
  (num-op * l r))

(module+ test
  (test (num+ (numV 1) (numV 2))
        (numV 3))
  (test (num* (numV 2) (numV 3))
        (numV 6)))

;; lookup ----------------------------------------
(define (lookup [n : Symbol] [env : Env]) : Value
  (type-case (Listof Binding) env
    [empty (error 'lookup "free variable")]
    [(cons b rst-env) (cond
                        [(symbol=? n (bind-name b))
                         (bind-val b)]
                        [else (lookup n rst-env)])]))

(module+ test
  (test/exn (lookup 'x mt-env)
            "free variable")
  (test (lookup 'x (extend-env (bind 'x (numV 8)) mt-env))
        (numV 8))
  (test (lookup 'x (extend-env
                    (bind 'x (numV 9))
                    (extend-env (bind 'x (numV 8)) mt-env)))
        (numV 9))
  (test (lookup 'y (extend-env
                    (bind 'x (numV 9))
                    (extend-env (bind 'y (numV 8)) mt-env)))
        (numV 8)))

;; ----------------------------------------
;; More encoding tests

(module+ test
  (test (interp (parse `{if true 1 2})
                mt-env)
        (numV 1))
  (test (interp (parse `{if false 1 2})
                mt-env)
        (numV 2))

  (test (interp (parse `{fst {pair 3 4}})
                mt-env)
        (numV 3))
  (test (interp (parse `{snd {pair 3 4}})
                mt-env)
        (numV 4))

  (test (interp (parse `{if {empty? empty} 1 0})
                mt-env)
        (numV 1))
  (test (interp (parse `{if {empty? {cons 1 empty}} 1 0})
                mt-env)
        (numV 0))
  (test (interp (parse `{if {cons? {cons 1 empty}} 1 0})
                mt-env)
        (numV 1))
  (test (interp (parse `{if {cons? empty} 1 0})
                mt-env)
        (numV 0))
  (test (interp (parse `{first {rest {cons 10 {cons 20 empty}}}})
                mt-env)
        (numV 20))
  (test (interp (parse `{if {empty? {rest {rest {cons 10 {cons 20 empty}}}}} 1 0})
                mt-env)
        (numV 1)))

;; ----------------------------------------
